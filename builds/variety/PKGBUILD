# Maintainer: Logan Marchione <logan (at) loganmarchione (dot) com>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

_gitname=variety
pkgname=${_gitname}-git
pkgver=0.8.5.r7.g98a5a0a
pkgrel=1
pkgdesc="Variety wallpaper changer (git version)"
arch=('any')
url="https://github.com/varietywalls/variety"
license=('GPL3')
depends=('python3' 'gtk3' 'imagemagick' 'libgexiv2' 'libnotify'
         'python-beautifulsoup4' 'python-lxml' 'python-cairo'
         'python-configobj' 'python-pillow' 'python-requests' 'python-dbus' 'python-gobject' 'python-httplib2'
         'python-pycurl')
makedepends=('git' 'python-distutils-extra')
optdepends=('xdg-user-dirs: set wallpaper for KDE Plasma')
provides=('variety')
conflicts=('variety')
source=("${pkgname}::git+https://github.com/varietywalls/variety" 'UnsplashAPIKey.patch' 'UnsplashAPIKey')
md5sums=('SKIP' 'SKIP' 'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"
  source "${srcdir}/UnsplashAPIKey"
  sed -i -e "s|\$UNSPLASH_API_KEY|$UNSPLASH_API_KEY|g" "${srcdir}/UnsplashAPIKey.patch"
  patch --strip=1 --directory ./ --input="${srcdir}/UnsplashAPIKey.patch"
}

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py install --root="${pkgdir}" --optimize=1 > /dev/null
}
